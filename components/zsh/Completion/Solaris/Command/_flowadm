#compdef flowadm

_flowadm() {

local -a subcmds tr props
local expl

_flowadm_flow(){
	compadd "$@" - $(flowadm show-flow -p -o flow)
}

_flowadm_flow_int(){
	compadd "$@" - $(flowadm show-flow -p -o flow)
	compadd "$@" - $(dladm show-phys -p -o device)
}

subcmds=(
	"show-flow"
	"add-flow"
	"remove-flow"
	"set-flowprop"
	"reset-flowprop"
	"show-flowprop"
	"show-usage"
)
	
tr=(
	"tcp"
	"udp"
	"sctp"
	"icmp"
	"icmpv6"
)

props=(
	"priority"
	"maxbw"
)

if [[ $service == "flowadm" ]]; then
	_arguments -C -A "-*" \
		'*::command:->subcmd' && return 0
	
	if (( CURRENT == 1 )); then
		_wanted commands expl "flowadm subcommand" compadd -a subcmds
		return
	fi
	service="$words[1]"
	curcontext="${curcontext%:*}=$service:"
fi
case $service in
	("show-flow")
	_arguments -A "-*" \
		'-o[specify field to display]:field:(flow link ipaddr proto port dsfield)' \
		'-p[parsable output]' \
		'-P[persistent flow property information]' \
		'-S[continuously display network utilization by flow]' \
		'-s[display flow statistics]' \
		'-i[specify interval (used with -s)]:interval' \
		- set1 \
		'-l[display information for link]:link or flow:_net_interfaces' \
		- set2 \
		':flow:_flowadm_flow' \
	;;
	("add-flow")
	_arguments -A "-*" \
		'-t[temporary changes - do not persist across reboots]' \
		'-R[alternate root dir]:alternate root:_files' \
		'-l[specify link to which flow will be added]:link:_net_interfaces' \
		'-a[specify attribute]:attribute:(local_ip= remote_ip= transport=$tr local_port= dsfield=)' \
		'-p[set property value]:property:(maxbw= priority=)' \
	;;
	("remove-flow")
	_arguments -A "-*" \
		'-t[temporary changes - do not persist across reboots]' \
		'-R[alternate root dir]:alternate root:_files' \
		- set1 \
		'-l[specify link from which flow will be deleted]:link:_net_interfaces' \
		- set2 \
		':flow:_flowadm_flow' \
	;;
	("set-flowprop")
	_arguments -A "-*" \
		'-t[temporary changes - do not persist across reboots]' \
		'-R[alternate root dir]:alternate root:_files' \
		'-p[set property value]:property:(maxbw= priority=)' \
		':flow:_flowadm_flow' \
	;;
	("reset-flowprop")
	_arguments -A "-*" \
		'-t[temporary changes - do not persist across reboots]' \
		'-R[alternate root dir]:alternate root:_files' \
		'-p[set property value]:property:(maxbw= priority=)' \
		':flow:_flowadm_flow' \
	;;
	("show-flowprop")
	_arguments -A "-*" \
		'-c[parsable output (requires -o)]' \
		'-P[persistent flow property information]' \
		'-p[property to show]:property:_values -s , "property" $props' \
		':flow:_flowadm_flow' \
	;;
	("show-usage")
	_arguments -A "-*" \
		'-a[display all historical network usage]' \
		'-s[start time for data display (YYYY.MM.DD,hh:mm:ss)]' \
		'-e[end time for data display (YYYY.MM.DD,hh:mm:ss)]' \
		'-f[Read extended accounting records of network flow usage from file]:file:_files' \
		':flow:_flowadm_flow' \
		- set1 \
		'-F[specifies the format of plotfile (gnuplot is only supported format)]' \
		'-p[outputs flow usage data to a file of the format specified by the -F option]:plotfile:_files' \
		- set2 \
		'-d[display date in the format DD/MM/YYYY]' \
 	;;

esac
}

_flowadm "$@"
